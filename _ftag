#compdef ftag

setopt localoptions warncreateglobal typesetsilent extendedglob

autoload -U is-at-least
typeset -a _arg_opts

if is-at-least 5.2; then
    _arg_opts=(-s -S -C)
else
    _arg_opts=(-s -C)
fi

# local context curcontext="$curcontext" state line ret=1
local state ret=1
typeset -a arguments

arguments=(
    {-w,--wutag}'[Also tag files with wutag]'
    {-q,--query}'[Start fzf with a query]:--Query--:->query'
    {-h,--help}'[Display a help message]'
    {-b,--boxes}'[Add a box aroud header with boxes]'
    {-c,--lolcat}'[Color to header with lolcat]'
    {-t,--toilet}'[Color and format header with toilet]'
    {-l,--local}'[Use tags in local directory]:local:->local_tags'
    {-z,--zoxide}'[Start zoxide to select directory to use tags]'
    {-j,--jump}'[Start zoxide to select directory to use tags]'
  )

_arguments "${_arg_opts[@]}" $arguments \
    && ret=0

# '*: :->tags'

case $state in
  query)
    local -a tags=( ${(@u)${(@s:,:)${(@f)"$(tag -Nl)"}}} )
    local -a gtags=( ${${(@f)"$(tag --usage)"}//(#m)*/${${(s: :)${MATCH}//[[:space:]]##/ }[2]}} )
    _alternative \
      'tags:--Local Tags --:compadd -a - tags' \
      'gtags:--Global Tags--:compadd -a - gtags' && \
          ret=0
  ;;
  local_tags)
    local tags=( ${(@u)${(@s:,:)${(@f)"$(tag -Nl)"}}} )
    _describe -t tags 'Local Tags' tags && \
        ret=0
  ;;
  *)
    typeset -a tags=( ${(@u)${(@s:,:)${(@f)"$(tag -Nl)"}}} )
    typeset -a gtags=(${${(@f)"$(tag --usage)"}//(#m)*/${${(s: :)${MATCH}//[[:space:]]##/ }[2]}})
    _alternative \
      'tags:--Local Tags --:compadd -a - tags' \
      'gtags:--Global Tags--:compadd -a - gtags' \
      'arguments:--Options--:compadd -a - arguments' && \
          ret=0
  ;;
esac

return $ret
